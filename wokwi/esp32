Diagrama disponibilizado dentro do read.me
Código:
#include <WiFi.h>
#include <PubSubClient.h>

// Configuração
const char* ssid = "Wokwi-GUEST";
const char* password = "";
const char* mqtt_server = "test.mosquitto.org"; // (container Docker)
const int mqtt_port = 1883;
const char* deviceId = "ball_sensor_01";
const float distance_m = 1.0; // distância entre sensores em metros

// Pinos dos sensores
const int pinSensor1 = 14; // primeiro sensor
const int pinSensor2 = 17; // segundo sensor

WiFiClient espClient;
PubSubClient client(espClient);

unsigned long t1 = 0;
unsigned long t2 = 0;
bool waitingForSecond = false;

void setup_wifi() {
  delay(10);
  WiFi.begin(ssid, password);
  unsigned long start = millis();
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    if (millis() - start > 15000) break;
  }
}

void reconnect() {
  while (!client.connected()) {
    String clientId = String(deviceId) + "-" + String(random(0xffff), HEX);
    if (client.connect(clientId.c_str())) {
    } else {
    delay(2000);
    }
  }
}

void publishSpeed(float speed_kmh) {
  char topic[128];
  snprintf(topic, sizeof(topic), "sensors/ball_speed/%s", deviceId);

  char payload[256];
  snprintf(payload, sizeof(payload), "{\"deviceId\":\"%s\",\"speed_kmh\":%.2f,\"ts\":%lu}", deviceId, speed_kmh, millis());
  
  client.publish(topic, payload);
}

void setup() {
  Serial.begin(115200);
  pinMode(pinSensor1, INPUT_PULLUP);
  pinMode(pinSensor2, INPUT_PULLUP);
  setup_wifi();
  client.setServer(mqtt_server, mqtt_port);
}

void loop() {
  if (!client.connected()) reconnect();
  client.loop();
  
  int s1 = digitalRead(pinSensor1);
  int s2 = digitalRead(pinSensor2);
  
  if (!waitingForSecond && s1 == LOW) {
  t1 = micros();
  waitingForSecond = true;
  Serial.println("Trigger 1");
}

if (waitingForSecond && s2 == LOW) {
  t2 = micros();
  waitingForSecond = false;
  
  float dt = (t2 - t1) / 1e6; // segundos
  if (dt <= 0) dt = 0.0001;
  float v_m_s = distance_m / dt;
  float v_kmh = v_m_s * 3.6;
  
  Serial.print("Velocidade (km/h): ");
  Serial.println(v_kmh);
  
  publishSpeed(v_kmh);
}

if (waitingForSecond && (micros() - t1) > 5e6) {
  waitingForSecond = false;
  }
}
